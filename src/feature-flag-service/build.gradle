plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.github.ben-manes.versions' version '0.52.0'
}

group = 'com.dynatrace.easytrade'
version = '1.0.0-easytrade'

java {
	sourceCompatibility = JavaVersion.VERSION_21
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.34'

	implementation 'com.google.code.gson:gson:2.13.1'

	compileOnly 'org.projectlombok:lombok:1.18.38'
	annotationProcessor 'org.projectlombok:lombok:1.18.38'

	implementation 'org.hibernate.validator:hibernate-validator:9.0.1.Final'
    // https://mvnrepository.com/artifact/jakarta.el/jakarta.el-api
	implementation 'jakarta.el:jakarta.el-api:6.0.1'


	// Fix vulnerable dependencies
	implementation 'ch.qos.logback:logback-core:1.5.18'
    implementation 'ch.qos.logback:logback-classic:1.5.18'

	// -- not direct dependencies but need bumps to patch vulns
    // -- can be removed once the parent packages upgrade

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    // https://mvnrepository.com/artifact/org.apache.tomcat.embed/tomcat-embed-core
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.43'
}

tasks.named('test') {
	useJUnitPlatform()
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}
