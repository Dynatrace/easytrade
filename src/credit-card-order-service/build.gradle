plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.github.ben-manes.versions' version '0.52.0'
}

group = 'com.dynatrace.easytrade'
version = '1.0.0-easytrade'
java {
    java.sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-params:5.13.3'
    testImplementation 'org.mockito:mockito-core:5.18.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.18.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-commons:1.13.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-engine:1.13.3'
    

    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-jsr310
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.19.1'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-xml
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.19.1'

    implementation 'com.microsoft.sqlserver:mssql-jdbc:12.10.1.jre11'

    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'

    //open feature
    implementation 'dev.openfeature:sdk:1.16.0'

    // -- not direct dependencies but need bumps to patch vulns
    // -- can be removed once the parent packages upgrade

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    // https://mvnrepository.com/artifact/org.apache.tomcat.embed/tomcat-embed-core
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.43'
}

test {
    useJUnitPlatform()
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}
